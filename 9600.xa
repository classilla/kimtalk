
	* = $1780

	; max 102 bytes
	; send a string terminated with the high bit set to the DECtalk,
	; followed by a comma and line feed. address should be in zp on entry.

	; (c)2023 cameron kaiser. all rights reserved. bsd license.

zp	= $f9
yp	= zp+2

ch	= $fe

strsend	sei	; paranoia
	lda #0
	sta $1741
	sta yp
	lda #$3f
	sta $1743
	lda #7
	sta $1742

sendl	ldy yp
	lda (zp),y
	bmi endstr
	jsr send
	inc yp
	bne sendl
endstr	and #$7f
	jsr send
	lda #","
	jsr send
	lda #10
	jsr send
	cli
	rts

	; 9600 bps on 1.000000MHz 6502 is 104.2 cycles/bit
	; (i.e., 1_000_000/9_600)

send	sta ch
	ldx #8

	; start bit
	lda $1742	; (4)
	and #$fe	; (2)
	sta $1742	; (4) bit live

	ldy #15
	jsr delayy	; 88
	
send8	; cribbed from kim-1 outch routine $1ea0
	and #$fe	; 2
	lsr ch		; 5
	adc #0		; 2
	sta $1742	; 4 - bit live

	jsr delay	; 83

	dex		; 2 
	bne send8	; if taken 3 if not taken 2
	; 4 or 5

	; stop bit
sends	lda $1742	; 4
	ora #$01	; 2
	nop		; 2
	nop		; 2
	sta $1742	; 4 - bit live

	ldy #16
	jmp delayy	; 93

delay	; plus 12 jsr and rts are 12 cycles combined
	; so cycles = 2 + y*2 + (y-1)*3 + 2
	ldy #14	
delayy	dey
	bne delayy	; each loop taken = 3, final branch = 2
	rts
